# Maintainer: Kohei Suzuki <eagletmt@gmail.com>

_commit=fa7484019d4321f8c0775a4811574dccd29bcaea
_shortcommit=fa7484019d
pkgname='ruby-trunk'
pkgver=20201103
pkgrel=1
pkgdesc='An object-oriented language for quick and easy programming'
arch=('i686' 'x86_64')
url='http://www.ruby-lang.org/en/'
depends=('gdbm' 'openssl' 'libffi' 'libyaml' 'gmp' 'zlib')
makedepends=('ruby')  # for baseruby
provides=("ruby=3.0.0" 'rubygems' 'ruby-irb' 'ruby-reline')
conflicts=('ruby' 'rubygems' 'ruby-irb' 'ruby-reline')
backup=('etc/gemrc')
install='ruby.install'
license=('BSD' 'custom')
options=('!emptydirs' '!strip' 'staticlibs')
source=("ruby-${_commit}.tar.gz::https://github.com/ruby/ruby/archive/${_commit}.tar.gz"
        'gemrc')

build() {
  cd ruby-${_commit}

  autoreconf -i
  cat > revision.h << EOS
#define RUBY_REVISION "${_shortcommit}"
#define RUBY_FULL_REVISION "${_commit}"
EOS
  PKG_CONFIG=/usr/bin/pkg-config ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sharedstatedir=/var/lib \
    --libexecdir=/usr/lib/ruby \
    --enable-shared \
    --disable-rpath \
    --with-dbm-type=gdbm_compat \
    --enable-debug-env \
    --disable-install-doc \
    CFLAGS="$CFLAGS -ggdb3"

  make
}

check() {
  cd ruby-${_commit}

  make test
}

package() {
  cd ruby-${_commit}

  make DESTDIR="${pkgdir}" install

  install -D -m644 ${srcdir}/gemrc "${pkgdir}/etc/gemrc"

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/ruby/LICENSE"
  install -D -m644 BSDL "${pkgdir}/usr/share/licenses/ruby/BSDL"
}

sha1sums=('942919a6c81915afb3ab8083f3ee64513fba99c2'
          'de4b760b7e2cd9af88ca67536ce37b950f1ee514')
