# Maintainer: Kohei Suzuki <eagletmt@gmail.com>

_commit=a12a453152dc551a5e4c92038b19594815cbc7d1
_revision=49790
_baseversion=2.3.0
pkgname='ruby-trunk'
pkgver=${_baseversion}r${_revision}
pkgrel=1
pkgdesc='An object-oriented language for quick and easy programming'
arch=('i686' 'x86_64')
url='http://www.ruby-lang.org/en/'
depends=('gdbm' 'openssl' 'libffi' 'libyaml' 'gmp' 'zlib')
makedepends=('ruby')  # for baseruby
provides=("ruby=${pkgver}" 'rubygems' 'rake')
conflicts=('ruby')
conflicts=('rake')
backup=('etc/gemrc')
install='ruby.install'
license=('BSD' 'custom')
options=('!emptydirs' '!strip' 'staticlibs')
source=("ruby-r${_revision}.tar.gz::https://github.com/ruby/ruby/archive/${_commit}.tar.gz"
        'gemrc')

build() {
  cd ruby-${_commit}

  autoreconf -i
  PKG_CONFIG=/usr/bin/pkg-config ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sharedstatedir=/var/lib \
    --libexecdir=/usr/lib/ruby \
    --enable-shared \
    --disable-rpath \
    --with-dbm-type=gdbm_compat \
    --enable-debug-env \
    --disable-install-doc \
    CFLAGS="$CFLAGS -ggdb3" \
    CPPFLAGS="$CPPFLAGS -DRUBY_REVISION=${_revision}"

  make
}

check() {
  cd ruby-${_commit}

  make test
}

package() {
  cd ruby-${_commit}

  make DESTDIR="${pkgdir}" install

  install -D -m644 ${srcdir}/gemrc "${pkgdir}/etc/gemrc"

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/ruby/LICENSE"
  install -D -m644 BSDL "${pkgdir}/usr/share/licenses/ruby/BSDL"
}

sha1sums=('206f65fe709784114ae1291705ad6f297642a5c6'
          'de4b760b7e2cd9af88ca67536ce37b950f1ee514')
