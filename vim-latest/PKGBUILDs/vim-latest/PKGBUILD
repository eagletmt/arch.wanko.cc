# Maintainer: Kohei Suzuki <eagletmt@gmail.com>

pkgname=vim-latest
_baseversion=8.0
_patchlevel=0003
pkgver=${_baseversion}.${_patchlevel}
pkgrel=1
pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor'
arch=(i686 x86_64)
license=('custom:vim')
url="http://www.vim.org"
depends=('gpm')
makedepends=('perl' 'python' 'python2' 'ruby' 'luajit')
conflicts=(vim vim-runtime)
provides=(vim=$pkgver vim-runtime=$pkgver)
source=(vim-$pkgver.tar.gz::https://github.com/vim/vim/archive/v$pkgver.tar.gz)

prepare() {
  cd "$srcdir/vim-$pkgver"

  sed -i 's|set dummy python;|set dummy python2;|g' src/auto/configure
  sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' src/feature.h
  sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' src/feature.h
}

build()
{
  cd "$srcdir/vim-$pkgver"

  ./configure --prefix=/usr --localstatedir=/var/lib/vim --mandir=/usr/share/man \
    --with-features=huge --enable-gpm --enable-acl --with-x=no --disable-gui \
    --enable-multibyte --enable-cscope --disable-netbeans \
    --enable-perlinterp=dynamic --enable-pythoninterp=dynamic --enable-python3interp=dynamic \
    --enable-rubyinterp=dynamic --enable-luainterp=dynamic --with-luajit \
    --with-compiledby='Kohei Suzuki <eagletmt@gmail.com>' \
    --disable-smack

  make
}

package() {
  cd "$srcdir/vim-$pkgver"
  make VIMRCLOC=/etc DESTDIR="$pkgdir" install

  cd "$pkgdir/usr/bin"
  rm ex view                # provided by (n)vi in core

  # delete some manpages
  cd "$pkgdir/usr/share/man"
  rm -f {*/,}man1/ex.1 {*/,}man1/view.1      # provided by (n)vi
  rm -f {*/,}man1/evim.1                     # this does not make sense in the console version

  local _shortver=${_baseversion/./}
  # patch runtime
  cd "$pkgdir/usr/share/vim/vim$_shortver/"
  sed -i "s/rpmsave/pacsave/;s/rpmnew/pacnew/;s/,\*\.ebuild/\0,PKGBUILD*,*.install/" filetype.vim

  # fix FS#17216
  sed -i 's|messages,/var|messages,/var/log/messages.log,/var|' \
    "$pkgdir/usr/share/vim/vim$_shortver/filetype.vim"


  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  cd "$pkgdir/usr/share/licenses/$pkgname"
  ln -s ../../vim/vim$_shortver/doc/uganda.txt license.txt
}

check() {
  cd "$srcdir/vim-$pkgver"
  make -j1 test
}

# vim:set ts=2 sw=2 et:

sha1sums=('77fac5302969a292ab89e827a36f3b278c99af9a')
