# $Id: PKGBUILD 144370 2015-10-19 11:15:40Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>

pkgname=tmux-cjkwidth
pkgver=2.1
pkgrel=1
pkgdesc='A terminal multiplexer'
url='http://tmux.github.io/'
arch=('i686' 'x86_64')
license=('BSD')
depends=('ncurses' 'libevent' 'libutempter')
source=(https://github.com/tmux/tmux/releases/download/$pkgver/tmux-$pkgver.tar.gz
	ncurses6-xterm-standout.patch
	LICENSE
	wcwidth.patch)
conflicts=('tmux')
provides=("tmux=$pkgver-$pkgrel")
md5sums=('74a2855695bccb51b6e301383ad4818c'
         '70347df93ed1922d6718d91491a7d9b4'
         '71601bc37fa44e4395580b321963018e'
         '03ab7dc4a8810b9885108673dcebbece')

prepare() {
	cd "$srcdir/tmux-${pkgver/_/}"
#	patch -Np1 -i ../ncurses6-xterm-standout.patch
	patch -Np1 -i "$srcdir/wcwidth.patch"
}

build() {
	cd "$srcdir/tmux-${pkgver/_/}"
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/tmux-${pkgver/_/}"
	make install DESTDIR=$pkgdir
	install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/tmux/LICENSE"

	install -dm755 "$pkgdir/usr/share/tmux/"
	install -m644 examples/* "$pkgdir/usr/share/tmux/"
	install -Dm644 examples/tmux.vim "$pkgdir/usr/share/vim/vimfiles/syntax/tmux.vim"

	install -d $pkgdir/usr/share/bash-completion/completions/
	mv $pkgdir/usr/share/tmux/bash_completion_tmux.sh $pkgdir/usr/share/bash-completion/completions/tmux
}
